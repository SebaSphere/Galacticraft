plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings minecraft.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    compileOnly(project(path: ":common")) {
        transitive = false
    }
    runtimeOnly(project(path: ":common", configuration: "transformForgeFakeMod")) {
        transitive = false
    }
    shadow(project(path: ":common", configuration: "transformForge")) {
        transitive = false
    }
}

loom {
    mixinConfig = "galacticraft-forge.mixins.json"
    silentMojangMappingsLicense()
}

architectury {
    platformSetupLoomIde()
}

configurations {
    shadow
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
    inputs.property "META-INF/mods.toml", project.version
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadow]
    classifier "shadow"
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
    archiveClassifier = "forge"
}
